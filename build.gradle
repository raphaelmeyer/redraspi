apply from: 'gradle/cmake.gradle'
apply from: 'gradle/rsync.gradle'
apply from: 'gradle/workspace.gradle'

class Workspace {
  String id
  String name
}

def raspbian = new Workspace(name: 'raspbian')

task raspbianWorkspace(type: WorkspaceTask) {
  dockerImage = 'raphaelmeyer/base:1.1.0'
  workspace = raspbian
}

def arm_bcm2708 = {
  image = 'raphaelmeyer/arm-bcm2708:1.0.0'
  sourceHost = 'source'
  sourceContainer = '/home/user/source'
}

task initializeWorkspace(type: CMakeTask, dependsOn: raspbianWorkspace) {
  configure arm_bcm2708
  workspace = raspbian
  command = [ '-DCMAKE_TOOLCHAIN_FILE=/opt/cmake/raspbian.cmake', '/home/user/source' ]
}

task build(type: CMakeTask, dependsOn: initializeWorkspace) {
  configure arm_bcm2708
  workspace = raspbian
  command = [ '--build', '.' ]
}

task clean(type: CMakeTask, dependsOn: initializeWorkspace) {
  configure arm_bcm2708
  workspace = raspbian
  command = [ '--build', '.', '--target', 'clean' ]
}

task syncRootfs(type: SyncRootfs) {
  host = 'rpi2_blue'
}

